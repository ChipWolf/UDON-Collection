# Extended Udon Assembly by Toocanzs
#.INIT_START
#// HEAP VARIABLES
#PUSH  SystemInt32 1
#SETHEAP "ONE"
#PUSH  SystemInt32 0
#SETHEAP "ZERO"
#// this sets the maximum player amount
#PUSH  SystemInt32 80
#SETHEAP "maxArraySize"
#// LABELS
#PUSHLABEL "continue_1_EXIT"
#SETHEAP "continue_1_ADDRESS"
#.INIT_END

.data_start

    # THIS
    thisBehaviour: %VRCUdonUdonBehaviour, this
    thisGameObject: %UnityEngineGameObject, this
    thisTransform: %UnityEngineTransform, this
    localPlayerAPI: %VRCSDKBaseVRCPlayerApi, null
    
    onPlayerJoinedPlayer: %VRCSDKBaseVRCPlayerApi, null
    onPlayerLeftPlayer: %VRCSDKBaseVRCPlayerApi, null
    
    # PRIVATE VARIABLES
    FALSE: %SystemBoolean, null
    ONE: %SystemInt32, null
    ZERO: %SystemInt32, null
    NULL: %SystemObject, null
    # loop stuff
    index: %SystemInt32, null
    condition: %SystemBoolean, null
    #
    maxArraySize: %SystemInt32, null
    currentPlayer: %VRCSDKBaseVRCPlayerApi, null
    nextPlayer: %VRCSDKBaseVRCPlayerApi, null
    foundLeftPlayer: %VRCSDKBaseVRCPlayerApi, null
    nextIndex: %SystemInt32, null
    # ADDRESSES
    continue_1_ADDRESS: %SystemUInt32, null
    
    # PUBLIC VARIABLES
    PLAYERS: %VRCSDKBaseVRCPlayerApiArray, null
    PLAYERS_AMOUNT: %SystemInt32, null
    
    .export PLAYERS
    .export PLAYERS_AMOUNT
.data_end

.code_start

    continue_1:
        JUMP_INDIRECT, continue_1_ADDRESS
        JUMP, 0xFFFFFF
    
    _start:
    #{
        #INITIALISE ARRAY
        PUSH, maxArraySize
        PUSH, PLAYERS
        EXTERN, "VRCSDKBaseVRCPlayerApiArray.__ctor__SystemInt32__VRCSDKBaseVRCPlayerApiArray"
        # Set localPlayerAPI
        #PUSH, localPlayerAPI
        #EXTERN, "VRCSDKBaseNetworking.__get_LocalPlayer__VRCSDKBaseVRCPlayerApi"
        #EXIT
        JUMP, 0xFFFFFF
    #}
    .export _start

    _onPlayerJoined:
    #{
        # Add player to the array
        PUSH, PLAYERS
        PUSH, onPlayerJoinedPlayer
        PUSH, PLAYERS_AMOUNT
        EXTERN, "VRCSDKBaseVRCPlayerApiArray.__SetValue__SystemObject_SystemInt32__SystemVoid"
        # increment PLAYERS_AMOUNT
        PUSH, PLAYERS_AMOUNT
        PUSH, ONE
        PUSH, PLAYERS_AMOUNT
        EXTERN, "SystemInt32.__op_Addition__SystemInt32_SystemInt32__SystemInt32"
        # LOG
        PUSH, thisBehaviour
        PUSH, "debugLog"
        EXTERN, "VRCUdonCommonInterfacesIUdonEventReceiver.__SendCustomEvent__SystemString__SystemVoid" 
        #EXIT
        JUMP, 0xFFFFFF
    #}
    .export _onPlayerJoined
    
    _onPlayerLeft:
    #{
        # search for the player who left in the array
        # INITIALISE INDEX
        PUSH, ZERO
        PUSH, index
        COPY
        loop_1:
        #{
            # get current player from PLAYERS array
            PUSH, PLAYERS
            PUSH, index
            PUSH, currentPlayer
            EXTERN, "VRCSDKBaseVRCPlayerApiArray.__GetValue__SystemInt32__SystemObject"
            # check if the currentPlayer is the player who left
            PUSH, currentPlayer
            PUSH, onPlayerLeftPlayer
            PUSH, foundLeftPlayer
            EXTERN, "VRCSDKBaseVRCPlayerApi.__Equals__SystemObject__SystemBoolean"
            # INCREMENT INDEX
            PUSH, index
            PUSH, ONE
            PUSH, index
            EXTERN, "SystemInt32.__op_Addition__SystemInt32_SystemInt32__SystemInt32"
            # Out of bounds protection
            PUSH, index
            PUSH, maxArraySize
            PUSH, condition
            EXTERN, "SystemInt32.__op_LessThan__SystemInt32_SystemInt32__SystemBoolean"
            PUSH, condition
            JUMP_IF_FALSE, 0xFFFFFF
            # if the player is found continue, else go back
            PUSH, foundLeftPlayer
            JUMP_IF_FALSE, loop_1
            # PLAYER FOUND
            loop_2:
            #{
                # get nextIndex
                PUSH, index
                PUSH, nextIndex
                COPY
                PUSH, nextIndex
                PUSH, ONE
                PUSH, nextIndex
                EXTERN, "SystemInt32.__op_Addition__SystemInt32_SystemInt32__SystemInt32"
                # check if index is at last pos
                PUSH, nextIndex
                PUSH, PLAYERS_AMOUNT
                PUSH, condition
                EXTERN, "SystemInt32.__op_Equality__SystemInt32_SystemInt32__SystemBoolean"
                PUSH, condition
                JUMP_IF_FALSE, continue_1
                # NOT AT LAST POSITION
                # copy element at index to nextIndex
                PUSH, PLAYERS
                PUSH, nextIndex
                PUSH, nextPlayer
                EXTERN, "VRCSDKBaseVRCPlayerApiArray.__GetValue__SystemInt32__SystemObject"
                PUSH, PLAYERS
                PUSH, nextPlayer
                PUSH, index
                EXTERN, "VRCSDKBaseVRCPlayerApiArray.__SetValue__SystemObject_SystemInt32__SystemVoid"
                # INCREMENT INDEX
                PUSH, index
                PUSH, ONE
                PUSH, index
                EXTERN, "SystemInt32.__op_Addition__SystemInt32_SystemInt32__SystemInt32"
                # check if index is at last pos
                PUSH, index
                PUSH, PLAYERS_AMOUNT
                PUSH, condition
                EXTERN, "SystemInt32.__op_Equality__SystemInt32_SystemInt32__SystemBoolean"
                PUSH, condition
                JUMP_IF_FALSE, loop_2
            #}
        #}
        continue_1_EXIT:
        # decrement PLAYERS_AMOUNT
        PUSH, PLAYERS_AMOUNT
        PUSH, ONE
        PUSH, PLAYERS_AMOUNT
        EXTERN, "SystemInt32.__op_Subtraction__SystemInt32_SystemInt32__SystemInt32"
        # REMOVE LAST ELEMENT
        PUSH, PLAYERS
        PUSH, NULL
        PUSH, PLAYERS_AMOUNT
        EXTERN, "VRCSDKBaseVRCPlayerApiArray.__SetValue__SystemObject_SystemInt32__SystemVoid"
        # LOG
        PUSH, thisBehaviour
        PUSH, "debugLog"
        EXTERN, "VRCUdonCommonInterfacesIUdonEventReceiver.__SendCustomEvent__SystemString__SystemVoid" 
        #EXIT
        JUMP, 0xFFFFFF
    #}
    .export _onPlayerLeft
    
    debugLog:
    #{
        PUSH, "============================================="
        EXTERN, "UnityEngineDebug.__Log__SystemObject__SystemVoid"
        PUSH, "======= PLAYER MANAGER ======================"
        EXTERN, "UnityEngineDebug.__Log__SystemObject__SystemVoid"
        # INITIALISE INDEX
        PUSH, ZERO
        PUSH, index
        COPY
        loop_3:
        #{
            # get current player from PLAYERS array
            PUSH, PLAYERS
            PUSH, index
            PUSH, currentPlayer
            EXTERN, "VRCSDKBaseVRCPlayerApiArray.__GetValue__SystemInt32__SystemObject"
            PUSH, currentPlayer
            EXTERN, "UnityEngineDebug.__Log__SystemObject__SystemVoid"
            # INCREMENT INDEX
            PUSH, index
            PUSH, ONE
            PUSH, index
            EXTERN, "SystemInt32.__op_Addition__SystemInt32_SystemInt32__SystemInt32"
            # check if index is at last pos
            PUSH, index
            PUSH, PLAYERS_AMOUNT
            PUSH, condition
            EXTERN, "SystemInt32.__op_Equality__SystemInt32_SystemInt32__SystemBoolean"
            # if the player is found continue, else go back
            PUSH, condition
            JUMP_IF_FALSE, loop_3
        #}
        PUSH, PLAYERS_AMOUNT
        EXTERN, "UnityEngineDebug.__Log__SystemObject__SystemVoid"
        PUSH, "============================================="
        EXTERN, "UnityEngineDebug.__Log__SystemObject__SystemVoid"
    #}
    .export debugLog
    
.code_end
